ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

apply plugin: 'java'
apply plugin: 'maven-publish'

if (isReleaseVersion) {
    apply plugin: 'signing'
}

group = 'com.epam.deltix'

sourceCompatibility = 8

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

sourceSets {
    main.java.srcDirs += [
        "$rootDir/java/dfp-math/build/generated/sources/nativeWrappers",
        "$rootDir/NativeUtils/java/main/src/main/java",
        "$rootDir/NativeUtils/Zstandard/java/src/main/java"]

    main.resources.srcDirs += "$rootDir/native/binmathJava/Release/"
}

dependencies {
    implementation project(':java:dfp')
    testImplementation project(':java:testUtils')
    testImplementation 'junit:junit:4.+'
    testImplementation 'org.apache.commons:commons-math3:3.6.1'
}

compileJava.dependsOn ":java:nativeWrappers:makeNativeWrappersDfpMath"

task copyNativeDfpMathResources(type: Copy) {
    from("$rootDir/native/binmath/Release")
    destinationDir(file("$rootDir/native/binmathJava/Release/resources_com_epam_deltix_dfpmath"))
    onlyIf {
        !file("$rootDir/native/binmathJava/Release/resources_com_epam_deltix_dfpmath").exists()
    }
}
compileJava.dependsOn(copyNativeDfpMathResources)

def rUser = findProperty('SONATYPE_NEXUS_USERNAME') ?: System.getenv('SONATYPE_NEXUS_USERNAME') ?: "FakeUser"
def rPass = findProperty('SONATYPE_NEXUS_PASSWORD') ?: System.getenv('SONATYPE_NEXUS_PASSWORD') ?: "FakePass"

publishing {
    repositories {
        maven {
            url = findProperty('SONATYPE_REPOSITORY') ?: System.getenv('SONATYPE_REPOSITORY') ?: "FakeRepo"

            credentials {
                username rUser
                password rPass
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'dfp-math'
                packaging = 'jar'
                description = 'Deltix Decimal Floating-Point Math Arithmetic Library.'
                url = 'https://github.com/epam/DFP'

                scm {
                    connection = 'scm:git:https://github.com/epam/DFP.git'
                    developerConnection = 'scm:git:https://github.com/epam/DFP.git'
                    url = 'https://github.com/epam/DFP.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'AndreiDavydov'
                        name = 'Andrei Davydov'
                        email = 'agdavydov81@gmail.com'
                        url = 'https://github.com/agdavydov81'
                        organization = 'EPAM Systems'
                        organizationUrl = 'https://www.epam.com/'
                    }
                }
            }
        }
    }
}

if (isReleaseVersion) {
    signing {
        def signingKey = findProperty('SIGNING_PRIVATE_KEY') ?: System.getenv('SIGNING_PRIVATE_KEY') ?: "FakeUser"
        def signingPassword = findProperty('SIGNING_PASSWORD') ?: System.getenv('SIGNING_PASSWORD') ?: "FakePass"

        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenJava

        required { isReleaseVersion }
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Deltix Decimal Floating-Point Math Arithmetic Library',
            'Implementation-Version': archiveVersion,
            'Main-Class': 'com.epam.deltix.dfpmath.Demo'
    }
    from("$rootDir") {
        include 'LICENSE'
    }
    from("$rootDir") {
        include 'intel-eula.txt'
    }
    setEntryCompression ZipEntryCompression.STORED
}

sourcesJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task testsJar(type: Jar, dependsOn: [jar, testClasses, processTestResources]) {
    archiveClassifier = 'tests'
    from sourceSets.test.output
}

task copyTestDeps(type: Copy) {
    from(sourceSets.test.runtimeClasspath) { include '*.jar' }
    into('testLibs')
}

task copyTestJars(type: Copy, dependsOn: [testsJar, copyTestDeps]) {
    from(jar.outputs.files)
    from(testsJar.outputs.files)
    into('testLibs')
}

task runTestJars(type: JavaExec) {
    mainClass = 'org.junit.runner.JUnitCore'
    classpath = files { file('testLibs').listFiles() }

    def testClassesRoot = file('src/test/java').absolutePath
    fileTree(dir: testClassesRoot, include: '**/*Test.java').each { File file ->
        def ap = file.absolutePath
        args += ap.substring(testClassesRoot.length() + 1, ap.length() - 5).replace(File.separator, '.')
    }
}
