plugins {
    id "me.champeau.gradle.jmh" version "0.5.3" apply false
}

apply plugin: 'java'
apply plugin: 'me.champeau.gradle.jmh'

group = 'com.epam.deltix'

sourceCompatibility = 1.7
compileTestJava {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
}
compileJmhJava {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
}

repositories {
    mavenCentral()
}

sourceSets {
    main.java.srcDirs += [
        "$rootDir/java/dfp/build/generated/sources/nativeWrappers",
        "$rootDir/NativeUtils/java/main/src/main/java",
        "$rootDir/NativeUtils/Zstandard/java/src/main/java"]

    main.resources.srcDirs += "$rootDir/native/binJava/Release/"

    test.java.srcDirs += [
        "$rootDir/java/dfp/src/testUtils/java"]
}

dependencies {
    implementation project(":java:dfp")
    testImplementation 'junit:junit:4.+'
    testImplementation 'org.apache.commons:commons-math3:3.6.1'
//    testImplementation 'com.google.guava:guava:31.1-jre'
}

compileJava.dependsOn ":java:nativeWrappers:makeNativeWrappersDfp"

task copyNativeDfpResources(type: Copy) {
    from("$rootDir/native/bin/Release")
    destinationDir(file("$rootDir/native/binJava/Release/resources_com_epam_deltix_dfp"))
    onlyIf {
        !file("$rootDir/native/binJava/Release/resources_com_epam_deltix_dfp").exists()
    }
}
compileJava.dependsOn(copyNativeDfpResources)

jmh {
    include = '.*Benchmark.*'
}

