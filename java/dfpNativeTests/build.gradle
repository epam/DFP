import java.nio.file.Files
import java.nio.file.Paths

plugins {
    id "me.champeau.jmh" version "0.7.1"
}

apply plugin: 'java'

group = 'com.epam.deltix'

sourceCompatibility = 8

repositories {
    mavenCentral()
}

sourceSets {
    test.java.srcDirs += [
        "$rootDir/java/dfp/build/generated/sources/nativeWrappers",
        "$rootDir/NativeUtils/java/main/src/main/java",
        "$rootDir/NativeUtils/Zstandard/java/src/main/java"]

    test.resources.srcDirs += "$rootDir/native/installJava/"
}

dependencies {
    implementation project(":java:dfp")
    testImplementation project(':java:testUtils')
    testImplementation 'junit:junit:4.+'
    testImplementation 'org.apache.commons:commons-math3:3.6.1'
//    testImplementation 'com.google.guava:guava:31.1-jre'
}

compileJava.dependsOn ":java:nativeWrappers:makeNativeWrappersDfp"

def versionSuffix = versioning()["suffix"]

task copyNativeDfpResourcesMuslToAmd64(type: Copy) {
    from "$rootDir/native/install/linux/musl-gcc/lib"
    into "$rootDir/native/installJava/resources_com_epam_deltix_dfp/linux/amd64"
    include "**/*ddfp${versionSuffix}.so.zst"
}

static void deleteEmptyFolders(String startingDir) {
    Files.walk(Paths.get(startingDir)).each { path ->
        if (Files.isDirectory(path) && Files.list(path).count() == 0) {
            Files.delete(path)
        }
    }
}

task copyNativeDfpResources(type: Copy, dependsOn: copyNativeDfpResourcesMuslToAmd64) {
    from "$rootDir/native/install"
    into "$rootDir/native/installJava/resources_com_epam_deltix_dfp"
    include "**/*ddfp${versionSuffix}.dylib.zst"
    include "**/*ddfp${versionSuffix}.so.zst"
    include "**/*ddfp${versionSuffix}.dll.zst"
    exclude 'linux/amd64/**'
    exclude 'linux/musl-gcc/**'

    eachFile { fileCopyDetails ->
        // Copy one level up from the lib or bin directory
        def relative = fileCopyDetails.relativePath
        fileCopyDetails.relativePath = relative.getParent().append(true, "..", relative.getLastName())
    }

    doLast {
        deleteEmptyFolders("$rootDir/native/installJava/resources_com_epam_deltix_dfp")
    }
}
compileJava.dependsOn(copyNativeDfpResources)

task testsJar(type: Jar, dependsOn: [jar, testClasses, processTestResources]) {
    archiveClassifier = 'tests'
    from sourceSets.test.output
}

task copyTestDeps(type: Copy) {
    from(sourceSets.test.runtimeClasspath) { include '*.jar' }
    into('testLibs')
}

task copyTestJars(type: Copy, dependsOn: [testsJar, copyTestDeps]) {
    from(jar.outputs.files)
    from(testsJar.outputs.files)
    into('testLibs')
}

task runTestJars(type: JavaExec) {
    mainClass = 'org.junit.runner.JUnitCore'
    classpath = files { file('testLibs').listFiles() }

    def testClassesRoot = file('src/test/java').absolutePath
    fileTree(dir: testClassesRoot, include: '**/*Test.java').each { File file ->
        def ap = file.absolutePath
        args += ap.substring(testClassesRoot.length() + 1, ap.length() - 5).replace(File.separator, '.')
    }
}
